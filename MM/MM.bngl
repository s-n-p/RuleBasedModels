# MM Example
begin model
begin parameters

# Avogadro’s number - scaled for umol
NA 6.022e23/1e6

# Cell volume
V 1e-12 # liters - typical for eukaryote

# Rate constants
kp1 1.0/(NA*V) # 1/uM 1/s -> 1/molecules 1/s
km1 0.1 # 1/s
k2 0.01 # 1/s
ksyn 1e-4*(NA*V) # uM/s -> molecules / s
kdeg 0.01 # 1/s

# Initial concentrations
E0 0.01*NA*V # uM -> molecules
S0 1.0*NA*V # uM -> molecules
end parameters

begin molecule types
E(s)
S(Y~0~P)
end molecule types

begin seed species
E(s) E0
S(Y~0) S0
end seed species

begin observables
Molecules SU S(Y~0)
Molecules SP S(Y~P)
Molecules ES E(s!1).S(Y!1)
Molecules Etot E()
end observables

begin reaction rules
ESbind: E(s) + S(Y~0) <-> E(s!1).S(Y~0!1) kp1,km1
ESconvert: E(s!1).S(Y~0!1) -> E(s) + S(Y~P) k2
Esyndeg: 0 <-> E(s) ksyn, kdeg
end reaction rules

end model

# actions
generate_network({overwrite=>1})
#visualize({type=>"regulatory",groups=>1,collapse=>1,\
#opts=>"opts.txt"})

simulate({method=>"ode",t_end=>20000,n_steps=>1000})

writeSBML()
